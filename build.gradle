plugins {
    id 'java'
    id "com.github.spotbugs" version "5.0.14"
    id "com.diffplug.spotless" version "6.10.0"
    id 'pl.allegro.tech.build.axion-release' version '1.15.3'
    id 'org.javamodularity.moduleplugin' version '1.8.12' apply false
}

scmVersion {
    tag {
        prefix = ''
    }
}

group 'it.jnrpe'
apply plugin: 'distribution'
project.version=scmVersion.version

subprojects {
    apply plugin: 'java'
    apply plugin: "org.javamodularity.moduleplugin"
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'com.diffplug.spotless'

    apply from: "$rootProject.projectDir/gradle/spotless.gradle"
    apply from: "$rootProject.projectDir/gradle/spotbugs.gradle"
    apply from: "$rootProject.projectDir/gradle/publishing.gradle"

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation(platform('org.junit:junit-bom:5.9.3'))
        testImplementation('org.junit.jupiter:junit-jupiter')
        testImplementation(project(':it.jnrpe.integration'))
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    }

    java {
        modularity.inferModulePath = true
        withJavadocJar()
        withSourcesJar()
    }

    jar {
        manifest {
            attributes 'Implementation-Version': archiveVersion
            attributes 'Implementation-Title': 'JNRPE'
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            showStandardStreams = true
            events "passed", "skipped", "failed"
        }
    }

    tasks {
        javadoc {
            failOnError = false
        }

        compileJava {
            options.javaModuleVersion = provider { scmVersion.version }
        }
    }
}
